#include<ContToDateA.afl>

// Szûrési feltétel
sig_up = C > MA(C,100)
         AND MA(C,20) / MA(C,200) > 1.1
         AND MA(C,100) > MA(C,200)
         AND MA(C,100) > Ref(MA(C,100),-1)
         AND NOT (StrRight(Name(),2) == "OB");

// A becslés napja
ye = Param("Year", 2008, 2000, 2009, 1);
mo = Param("Month", 6, 1, 12, 1);
da = Param("Day", 15, 1, 31, 1);
mask = Year() == ye AND Month() == mo AND Day() == da;
i = Cum(mask*BarIndex()); i = LastValue(i);

Filter = sig_up[i];

// Idõ- és árvektor
t = Year() + DayOfYear() / (365 + (Year()%4 == 0 AND Year()%100 != 0)); // Szökõév figyelembe véve
pt = C; // Záróár

// Paraméterek
in_tt   = Param("Insample Length (Periods)", 150, 10, 600, 5);
out_tt  = Param("Outsample Length (Periods)", 50, 5, 200, 5);
beta_lo = Param("Beta Minimum", 0.15, 0.01, 0.20, 0.01);
beta_hi = Param("Beta Maximum", 0.85, 0.80, 0.99, 0.01);
anti    = (ParamToggle("Estimation Type", "Anti-Bubble|Bubble", defaultval =1 )-0.5)*2;
grd     = Param("Grid Density", 5, 1, 12, 1);

// Ellenõrzés in_tt hosszára
if (i < in_tt - 1) in_tt = i + 1;

// Eredményváltozók inicializálása
a0 = Null; b0 = Null; c0 = Null; tc0 = Null; beta0 = Null; omega0 = Null; phi0 = Null; ssr0 = Null; se0 = Null; rsq0 = Null; type0 = Null; ishighs0 = Null;
sig_rsq = 0; sig_tcin = 0; sig_ishighs = 0; sig_a = 0;
  
// Sornette számolás és eredmények
if(sig_up[i]) 
{
  outflag  = SorRoll(t, pt, mask, in_tt, out_tt, beta_lo, beta_hi, anti, grd);
  ishighs0 = omega0/(2*3.1415926)*anti*log((tc0-t[i-in_tt+1])/(tc0-t[i]));

  sig_rsq     = rsq0[i] >= 0.920; // R-négyzet szabály belépéshez
  sig_tcin    = tc0[i] - t[i] >= 30/250; // tc távolsága szabály belépéshez
  sig_ishighs = 1.75 <= ishighs0[i] AND ishighs0[i] <= 3.0;
  sig_a       = a0[i] > 1.2*C[i];
}

// Output
AddColumn(a0, "A");
AddColumn(b0, "B");
AddColumn(c0, "C");
AddColumn(IIf(mask,ContToDateA(tc0),Null), "Tc", formatDateTime , colorDefault, IIf(type0 == 2, colorRose, colorDefault));
AddColumn(beta0, "Beta", 1.3, colorDefault, IIf(beta0>beta_lo AND beta0<beta_hi, colorDefault, colorRose));
AddColumn(omega0, "Omega", 1.3, colorDefault, IIf(omega0>5 AND omega0<15, colorDefault, colorRose));
AddColumn(phi0, "Phi", 1.3);
AddColumn(ssr0, "SSR");
AddColumn(se0, "S.E.");
AddColumn(rsq0, "R-sq", 1.3, colorDefault, IIf(rsq0 <= 0.900, colorRose, IIf(rsq0 >= 0.950, colorPaleGreen, colorDefault)));
AddColumn(type0, "Type", 1.0, colorDefault, IIf( type0==2, colorRose, IIf(type0==1, colorDefault, colorPaleGreen)));
AddColumn(ishighs0, "IS Highs", 1.2, colorDefault, IIf(1.75 <= ishighs0 AND ishighs0 <= 3.0, colorPaleGreen, colorDefault));

AddColumn(IIf(mask AND sig_rsq, 1, Null), "R-sq", 1.0, colorDefault, IIf(mask AND sig_rsq, colorBrightGreen, colorDefault));
AddColumn(IIf(mask AND sig_tcin, 1, Null), "Tc In", 1.0, colorDefault, IIf(mask AND sig_tcin, colorBrightGreen, colorDefault));
AddColumn(IIf(mask AND sig_up, 1, Null), "Uptrend", 1.0, colorDefault, IIf(mask AND sig_up, colorBrightGreen, colorDefault));
AddColumn(IIf(mask AND sig_ishighs, 1, Null), "IS Highs", 1.0, colorDefault, IIf(mask AND sig_ishighs, colorBrightGreen, colorDefault));
AddColumn(IIf(mask AND sig_a, 1, Null), "A High", 1.0, colorDefault, IIf(mask AND sig_a, colorBrightGreen, colorDefault));